# To profile zsh in case of slow startup, uncomment the line below and move the
# second command to the bottom of this file
# zmodload zsh/zprof
# zprof will be called at the end of this file

export EDITOR="nvim"
export LANG=en_US.UTF-8

setopt EXTENDED_HISTORY
setopt HIST_EXPIRE_DUPS_FIRST
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_SAVE_NO_DUPS

PLUGIN_DIR="$HOME/.config/zsh/plugins"
_load_plugin() {
  local plugin_path="$1"
  if [[ -f "$plugin_path" ]]; then
    # shellcheck source=/dev/null
    source "$plugin_path"
  fi
}

# This function will set up two important environment variables:
# $FILE_DIR: Absolute path to the .zshrc file
# $DOT_SOURCE: Absolute path where dotfiles repo is cloned
# If the script fails to determine the path of dotfiles, it will print an error
# since a lot of functionalities will start failing (tmux, yabai, skhd, etc)
function _set_dot_source() {
  local absolute_file_path
  case ${OSTYPE} in
  darwin*)
    absolute_file_path="$(readlink "$HOME"/.zshrc)"
    FILE_DIR=${absolute_file_path%??????}
    # shellcheck source=/dev/null
    source "$FILE_DIR/macosrc"
    ;;
  linux*)
    absolute_file_path="$(readlink -f "$HOME"/.zshrc)"
    FILE_DIR=${absolute_file_path%??????}
    # shellcheck source=/dev/null
    source "$FILE_DIR/linuxrc"
    ;;
  esac
  export FILE_DIR
  export DOT_SOURCE="${FILE_DIR%/*}"
}

_set_dot_source
unfunction _set_dot_source

# Setup completion system
# Add plugin directories to fpath before compinit
# shellcheck disable=SC2206
fpath=("$PLUGIN_DIR/zsh-completions/src" $fpath)
autoload -Uz compinit
if [ -f ~/.zcompdump ]; then
  if [ -n "$(find ~/.zcompdump -mtime +0 2>/dev/null)" ]; then
    compinit
  else
    compinit -C
  fi
else
  compinit
fi
zstyle ':completion:*' menu yes select

eval "$(zoxide init zsh)"

FPATH="$(brew --prefix)/share/zsh/site-functions:${FPATH}"

# don't expand aliases _before_ completion has finished
setopt complete_aliases
zstyle ':completion:*' completer _expand_alias _complete _ignored
zstyle ':completion:*' regular true

eval "$(starship init zsh)"
_load_plugin "$PLUGIN_DIR/fzf-tab/fzf-tab.plugin.zsh"
_load_plugin "$PLUGIN_DIR/zsh-autosuggestions/zsh-autosuggestions.zsh"
_load_plugin "$PLUGIN_DIR/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
_load_plugin "$PLUGIN_DIR/zsh-interactive-cd/zsh-interactive-cd.plugin.zsh"

function _load_lazy_plugins() {
  export FORGIT_INSTALL_DIR="$PLUGIN_DIR/forgit"
  _load_plugin "$PLUGIN_DIR/forgit/forgit.plugin.zsh"
  _load_plugin "$PLUGIN_DIR/you-should-use/you-should-use.plugin.zsh"

  # Remove this hook after loading once
  add-zsh-hook -d precmd _load_lazy_plugins
  unfunction _load_lazy_plugins
}

# Load lazy plugins on first prompt (precmd runs before each prompt)
autoload -Uz add-zsh-hook
add-zsh-hook precmd _load_lazy_plugins

if command -v atuin &>/dev/null; then
  eval "$(atuin init zsh --disable-up-arrow)"
  bindkey '^R' atuin-search
fi

# Homebrew completions. Refer https://docs.brew.sh/Shell-Completion
if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH
fi

# Disable that pesky auto correct
unsetopt correct_all
unsetopt nomatch

# shellcheck source=/dev/null
source "$FILE_DIR/common_aliasrc"

# shellcheck source=/dev/null
source "$FILE_DIR/keybindingsrc"

# shellcheck source=/dev/null
source "$FILE_DIR/secretsrc"

# Lazy-load thefuck on first use
fuck() {
  unfunction fuck
  eval "$(thefuck --alias)"
  fuck "$@"
}

# Remove duplicate entries from PATH (zsh-specific)
# shellcheck disable=SC2034
typeset -U path
export PATH
